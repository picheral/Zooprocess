//------------- Zooprocess_pid_to_ecotaxa_uvp5 ---------------
version = "7.21";
date = "2015/09/17";

arg = 		getArgument(); 
array = 	split(arg," ");
projfolder = 		array[0];
pid_folder= 		array[1];
pid_file = 		array[2];
process_opt = 		array[3];
outlines = 		array[4];
status = 		array[5];
option = 		array[6];

ecotaxa_folder = pid_folder;
path_work = projfolder+"\\work";

// -------------- Repertoire ecotaxa ----------------
ecotaxa_root_folder = projfolder+"\\ecotaxa\\";
if ( File.exists(ecotaxa_root_folder) == false)	File.makeDirectory(ecotaxa_root_folder);
setBatchMode(true);

// ---------- Ouverture PID -----------------------
open(pid_folder+"\\"+pid_file);
wait(100);
data_read = getInfo();
run("Close");
data = split(data_read,"\n");
pid = false;

// --------------- ANNOTATION -----------------------
annotation_list_A = newArray(6);
annotation_list_B = newArray(6);
annotation_list_A[0] = "object_annotation_date";		annotation_list_B[0] = "[t]";
annotation_list_A[1] = "object_annotation_time";		annotation_list_B[1] = "[t]";
annotation_list_A[2] = "object_annotation_category";		annotation_list_B[2] = "[t]";
annotation_list_A[3] = "object_annotation_person_name";		annotation_list_B[3] = "[t]";
annotation_list_A[4] = "object_annotation_person_email";	annotation_list_B[4] = "[t]";
annotation_list_A[5] = "object_annotation_status";		annotation_list_B[5] = "[t]";

// ---------- Vérification de l'état de validation -----
annotation_list_C = newArray(6);
for (i = 0; i < annotation_list_C.length; i++) {	annotation_list_C[i] = "NaN";	}// for
if (endsWith(pid_file,"pid")) { annotation_list_C[5] = "noid";	pid = true;	}

// ---------- Recherche data section ---------------
// la ligne data_deb est la ligne d'entête des variables
i = 0;
while (i < data.length) {
	// ----------- Recherche si valide ----------------
	if (startsWith(data[i],"[Validation]")) annotation_list_C[5] = "validated";
	if (startsWith(data[i],"First_name")) { val = replace(data[i],"="," ");	
		text = split(val,"\ ");	
		if (text.length > 1) val_first = text[text.length-1];
	}//
	if (startsWith(data[i],"Last_name")) { val = replace(data[i],"="," ");	
		text = split(val,"\ ");	
		if (text.length > 1) val_last = text[text.length-1];
		annotation_list_C[3] =  val_first+" "+val_last;
	}//
	if (startsWith(data[i],"Validation_upload_time")) { val = replace(data[i],"="," ");	
		text = split(val,"\ ");	
		if (text.length > 1) val = text[text.length-1];
		annotation_list_C[0] = substring(val,0,8);
		annotation_list_C[1] = substring(val,9,13)+"00";
	}//
	if (startsWith(data[i],"[Data]")) { data_deb = i+1; 	i = data.length; 	}//if
	i++;
}// while
if (annotation_list_C[3] == "marc picheral" ) annotation_list_C[4] = "marc.picheral@obs-vlfr.fr";

// ------------ si pas PID et pas Validated ----------------------
if (annotation_list_C[5] == "NaN" ) annotation_list_C[5] = "predicted";

// ---------- Liste des champs ---------------------
img_list_A = newArray(10);
img_list_B = newArray(10);
img_list_C = newArray(10);
img_list_A[0] = "img_file_name";	img_list_C[0] = "[t]";
img_list_A[1] = "img_rank";		img_list_C[1] = "[f]";

// -------------- OBJECT ----------------------------
object_list_A = newArray(8);
object_list_B = newArray(8);
object_list_C = newArray(8);
for (i = 0; i < object_list_B.length; i++) {	object_list_B[i] = "NaN";	object_list_C[i] = "NaN";	object_list_C[i] = "[t]";}// for
object_list_A[0] = "object_id";			
object_list_A[1] = "object_link";		
object_list_A[2] = "object_lat";		object_list_C[2] = "[f]";	
object_list_A[3] = "object_lon";		object_list_C[3] = "[f]";		
object_list_A[4] = "object_date";		
object_list_A[5] = "object_time";		
object_list_A[6] = "object_depth_min";		
object_list_A[7] = "object_depth_max";		
object_list_B[1] = "http://www.lov.obs-vlfr.fr/fr/moyens_a_la_recherche/plate_forme_imagerie.html/";

// --------------------------- Variables dérivées -------------------------------------
objet_derived_A = newArray(13);
objet_derived_B = newArray(13);
objet_derived_C = newArray(13);
for (i = 0; i < objet_derived_C.length; i++) {	objet_derived_C[i] = "NaN";		objet_derived_B[i] = "[f]"; }// for
objet_derived_A[0] = "object_esd";					
objet_derived_A[1] = "object_elongation";				
objet_derived_A[2] = "object_range";					
objet_derived_A[3] = "object_meanpos";				
objet_derived_A[4] = "object_centroids";				
objet_derived_A[5] = "object_cv";					
objet_derived_A[6] = "object_sr";					
objet_derived_A[7] = "object_perimareaexc";				
objet_derived_A[8] = "object_feretareaexc";				
objet_derived_A[9] = "object_perimferet";				
objet_derived_A[10] = "object_perimmajor";				
objet_derived_A[11] = "object_circex";					
objet_derived_A[12] = "object_cdexc";					

// -------------- ACQU -------------------------------------
acqu_list_A = newArray(17);
acqu_list_B = newArray(17);
acqu_list_C = newArray(17);
for (i = 0; i < acqu_list_B.length; i++) {	acqu_list_B[i] = "NaN";	acqu_list_C[i] = "[f]";	}// for
acqu_list_A[0] = "acq_id";			acqu_list_C[0] = "[t]";	//
acqu_list_A[1] = "acq_sn";			acqu_list_C[1] = "[t]";	//
acqu_list_A[2] = "acq_volimage";		
acqu_list_A[3] = "acq_aa";			
acqu_list_A[4] = "acq_exp";			
acqu_list_A[5] = "acq_pixel";			
acqu_list_A[6] = "acq_file_description";	acqu_list_C[6] = "[t]";	//
acqu_list_A[7] = "acq_tasktype";		
acqu_list_A[8] = "acq_disktype";		
acqu_list_A[9] = "acq_shutterspeed";		
acqu_list_A[10] = "acq_gain";			
acqu_list_A[11] = "acq_threshold";		
acqu_list_A[12] = "acq_smbase";			
acqu_list_A[13] = "acq_smzoo";			
acqu_list_A[14] = "acq_erase_border_blob";	
acqu_list_A[15] = "acq_choice";			
acqu_list_A[16] = "acq_ratio";			

// ---------------- SAMPLE -------------------------------------
sample_list_A = newArray(17);
sample_list_B = newArray(17);
sample_list_C = newArray(17);
for (i = 0; i < sample_list_B.length; i++) {	sample_list_B[i] = "NaN";	sample_list_C[i] = "[t]";	}// for
sample_list_A[0] = "sample_id";			
sample_list_A[1] = "sample_profileid";		
sample_list_A[2] = "sample_cruise";		
sample_list_A[3] = "sample_ship";		
sample_list_A[4] = "sample_stationid";		
sample_list_A[5] = "sample_bottomdepth";	
sample_list_A[6] = "sample_ctdrosettefilename";	
sample_list_A[7] = "sample_dn";			
sample_list_A[8] = "sample_winddir";		sample_list_C[8] = "[f]";//
sample_list_A[9] = "sample_windspeed";		sample_list_C[9] = "[f]";//	
sample_list_A[10] = "sample_seastate";		sample_list_C[10] = "[f]";//
sample_list_A[11] = "sample_nebuloussness";	sample_list_C[11] = "[f]";	////
sample_list_A[12] = "sample_yoyo";		
sample_list_A[13] = "sample_comment";	

// ------------------- Ligne à formater selon ALTIDEV ----------------
sample_list_A[14] = "sample_dataportal_tara_trait";		
sample_list_A[15] = "sample_dataportal_tara_descripteur";	
sample_list_A[16] = "sample_barcode";

// ------------------ PROCESS --------------------------------------
process_list_A = newArray(13);
process_list_B = newArray(13);
process_list_C = newArray(13);
for (i = 0; i < process_list_B.length; i++) {	process_list_B[i] = "NaN";	process_list_C[i] = "[t]";	}// for
process_list_A[0] = "process_id";		
process_list_A[1] = "process_date";		
process_list_A[2] = "process_time";		
process_list_A[3] = "process_software";		
process_list_A[4] = "process_first_img";	process_list_C[4] = "[f]";//
process_list_A[5] = "process_last_img";		process_list_C[5] = "[f]";//
process_list_A[6] = "process_pressure_gain";	process_list_C[6] = "[f]";//
process_list_A[7] = "process_calibration";	
process_list_A[8] = "process_pixel";		process_list_C[8] = "[f]";//
process_list_A[9] = "process_upper";		process_list_C[9] = "[f]";//
process_list_A[10] = "process_gamma";		process_list_C[10] = "[f]";//
process_list_A[11] = "process_esdmin";		process_list_C[11] = "[f]";//
process_list_A[12] = "process_esdmax";		process_list_C[12] = "[f]";

// ---------- paramètres "fixes" -------------------
Acq_id = "uvp5";
Process_id = "zooprocess";
Process_software = "zooprocess";

acqu_list_B[1] = Acq_id;
process_list_B[0] = Process_id;
process_list_B[3] = Process_software;

aa = indexOf(pid_folder,"uvp5");
ligne = substring(pid_folder,aa,lengthOf(pid_folder));
bb = indexOf(ligne,"_");
ligne = substring(ligne,bb+1,lengthOf(ligne));
cc = indexOf(ligne,"_");
acqu_list_B[1] = substring(ligne,0,cc);

// ---------- recherche des paramètres -------------
for (i = 1; i < data_deb;i++) {
	text = replace(data[i],"="," "); text = split(text,"\ "); 
	if (text.length > 1) val = text[text.length-1];
	if (startsWith(data[i],"cruise=")) { 			sample_list_B[2] = toString(val);	
	}//if
	else if (startsWith(data[i],"filename=")) { 		sample_list_B[0] = "hdr"+toString(val); 	
								object_list_B[4] = substring(val,0,8);	
								object_list_B[5] = substring(val,8,14);				
	}//if
	else if (startsWith(data[i],"profileid=")) { 		sample_list_B[1] = toString(val); 						}//if	
	else if (startsWith(data[i],"ship=")) { 		sample_list_B[3] = toString(val); 							}//if
	else if (startsWith(data[i],"stationid=")) { 		sample_list_B[4] = toString(val); 						}//if
	else if (startsWith(data[i],"bottomdepth=")) { 		sample_list_B[5] = toString(val); 						}//if
	else if (startsWith(data[i],"ctdrosettefilename=")) { 	sample_list_B[6] = toString(val); 						}//if
	else if (startsWith(data[i],"dn=")) { 			sample_list_B[7] = toString(val); 							}//if
	else if (startsWith(data[i],"winddir=")) { 		sample_list_B[8] = toString(val); 						}//if
	else if (startsWith(data[i],"windspeed=")) { 		sample_list_B[9] = toString(val); 						}//if
	else if (startsWith(data[i],"seastate=")) { 		sample_list_B[10] = toString(val); 						}//if
	else if (startsWith(data[i],"nebuloussness=")) { 	sample_list_B[11] = toString(val); 					}//if
	else if (startsWith(data[i],"yoyo=")) { 		sample_list_B[12] = toString(val); 							}//if
	else if (startsWith(data[i],"comment=")) { 		sample_list_B[13] = toString(val); 						}//if
	
	else if (startsWith(data[i],"Date=")) { 		process_list_B[1] = substring(val,0,8); 
								process_list_B[2] = substring(val,9,13)+"00"; 
	}// if
	else if (startsWith(data[i],"First_image=")) { 		process_list_B[4] = toString(val); 						}//if	
	else if (startsWith(data[i],"Last_image=")) { 		process_list_B[5] = toString(val); 						}//if	
	else if (startsWith(data[i],"pressure_gain=")) { 	process_list_B[6] = toString(val); 					}//if	
	else if (startsWith(data[i],"calibration=")) { 		process_list_B[7] = toString(val); 						}//if	
	else if (startsWith(data[i],"pixel=")) { 		process_list_B[8] = toString(val); 						}//if	
	else if (startsWith(data[i],"upper=")) { 		process_list_B[9] = toString(val); 						}//if	
	else if (startsWith(data[i],"gamma=")) { 		process_list_B[10] = toString(val); 						}//if	
	else if (startsWith(data[i],"esdmin=")) { 		process_list_B[11] = toString(val); 						}//if	
	else if (startsWith(data[i],"esdmax=")) { 		process_list_B[12] = toString(val); 						}//if		
 		
	else if (startsWith(data[i],"volimage=")) { 		acqu_list_B[2] = toString(val); 						}//if	
	else if (startsWith(data[i],"aa=")) { 			acqu_list_B[3] = toString(val); 						}//if	
	else if (startsWith(data[i],"exp=")) { 			acqu_list_B[4] = toString(val); 						}//if	
	else if (startsWith(data[i],"pixel=")) { 		acqu_list_B[5] = toString(val); 						}//if	
	else if (startsWith(data[i],"File_description=")) { 	acqu_list_B[6] = toString(val); 						}//if	
	else if (startsWith(data[i],"TaskType=")) { 		acqu_list_B[7] = toString(val); 						}//if	
	else if (startsWith(data[i],"DiskType=")) { 		acqu_list_B[8] = toString(val); 						}//if	
	else if (startsWith(data[i],"ShutterSpeed=")) { 	acqu_list_B[9] = toString(val); 						}//if	
	else if (startsWith(data[i],"Gain=")) { 		acqu_list_B[10] = toString(val); 						}//if	
	else if (startsWith(data[i],"Threshold=")) { 		acqu_list_B[11] = toString(val); 						}//if	
	else if (startsWith(data[i],"SMbase=")) { 		acqu_list_B[12] = toString(val); 						}//if	
	else if (startsWith(data[i],"SMzoo=")) { 		acqu_list_B[13] = toString(val); 						}//if	
	else if (startsWith(data[i],"EraseBorderBlobs=")) { 	acqu_list_B[14] = toString(val); 				}//if	
	else if (startsWith(data[i],"Choice=")) { 		acqu_list_B[15] = toString(val); 				}//if	
	else if (startsWith(data[i],"Ratio=")) { 		acqu_list_B[16] = toString(val); 				}//if	

	else if (startsWith(data[i],"latitude=")) { 	oo = val; sign = 1;	
		if (oo < 0) sign = -1;
		oo = abs(oo);
		oo_int = floor(oo);
		object_list_B[2] = sign * (oo_int+100*(oo - oo_int)/60);
	}//if		
	else if (startsWith(data[i],"longitude=")) { 		oo = val; sign = 1;	                                                               
		if (oo < 0) {	sign = -1;	}//
		oo = abs(oo);
		oo_int = floor(oo);
		object_list_B[3] = sign * (oo_int+100*(oo - oo_int)/60);
	}//if	
}
// --------------------- Ecriture du fichier ----------------------
// ----------------------------------------------------------------
if (isOpen("Log")) {	selectWindow("Log"); run("Close");;	}//

// ----------------- Repertoire final ----------------------------------------------------
if (process_opt == "text") {
	// --------------- Creation du répertoire EcoTaxa Valide -------------
	pid_file_short = substring(pid_file,0,lengthOf(pid_file)-9);
	ecotaxa_folder = projfolder+"\\ecotaxa\\"+pid_file_short;
	File.makeDirectory(ecotaxa_folder);
}//

// ----------------- Entete ---------------------
ligne = "img_file_name"+"\t"+"img_rank";
for(i=0; i<object_list_A.length ;i++) {		ligne = ligne +"\t"+object_list_A[i];			} // if	
// ---------------- Lire entete data -----------------------------
entete = data[data_deb];
field_sup_nb = 2;
if (indexOf(entete,"Status") > 0) field_sup_nb = 4;

//-----------------Recherche Type de separateur ( tabs, ; ou space)--------------------------
sep = "tabs";
ctrl1 = indexOf(entete,";");
if (ctrl1>=0) { 		sep = "dotcoma";	} // if
ctrl1 = indexOf(entete," ");
if (ctrl1>=0) { 		sep = "space";		} // if
if (sep == "dotcoma") {	entete= replace(entete,";"," ");	entete = split(entete,"\ ");		} // if
else if (sep == "tabs") {					entete = split(entete,"\t");		} // if
else if (sep == "space") {					entete = split(entete,"\ ");		} // if
long = entete.length;
if (annotation_list_C[5] == "validated") long = entete.length - field_sup_nb;
for (k = 3; k<long ;k++) {			ligne = ligne +"\t"+"object_"+toLowerCase(entete[k]);	} // for
for(i=0;i<objet_derived_A.length;i++) {		ligne = ligne +"\t"+toLowerCase(objet_derived_A[i]);	} // if	
for(i=0;i<annotation_list_A.length;i++) {	ligne = ligne +"\t"+toLowerCase(annotation_list_A[i]);	} // if
for(i=0;i<process_list_A.length;i++) {		ligne = ligne +"\t"+toLowerCase(process_list_A[i]);	} // if
for(i=0;i<acqu_list_A.length;i++) {		ligne = ligne +"\t"+toLowerCase(acqu_list_A[i]);	} // if
for(i=0;i<sample_list_A.length;i++) {		ligne = ligne +"\t"+toLowerCase(sample_list_A[i]);	} // if
print(ligne);
// ----------------- Entete ---------------------
ligne = img_list_C[0]+"\t"+img_list_C[1];
for(i=0; i<object_list_C.length ;i++) {		ligne = ligne +"\t"+object_list_C[i];			} // if	
// ---------------- Lire unites format -----------------------------
entete = data[data_deb];
if (sep == "dotcoma") {	entete= replace(entete,";"," ");	entete = split(entete,"\ ");		} // if
else if (sep == "tabs") {					entete = split(entete,"\t");		} // if
else if (sep == "space") {					entete = split(entete,"\ ");		} // if
long = entete.length;
if (annotation_list_C[5] == "validated") 	long = entete.length - field_sup_nb;
for (k = 3; k<long ;k++) {			ligne = ligne +"\t"+"[f]";	} // for
for(i=0;i<objet_derived_B.length;i++) {		ligne = ligne +"\t"+toLowerCase(objet_derived_B[i]);	} // if	
for(i=0;i<annotation_list_B.length;i++) {	ligne = ligne +"\t"+toLowerCase(annotation_list_B[i]);	} // if
for(i=0;i<process_list_C.length;i++) {		ligne = ligne +"\t"+toLowerCase(process_list_C[i]);	} // if
for(i=0;i<acqu_list_C.length;i++) {		ligne = ligne +"\t"+toLowerCase(acqu_list_C[i]);	} // if
for(i=0;i<sample_list_C.length;i++) {		ligne = ligne +"\t"+toLowerCase(sample_list_C[i]);	} // if
print(ligne);

// ----------------- Data ---------------------
nb_false = 0;
data_ligne = newArray(data.length - data_deb);
m = 0;
for (j = data_deb+1;j < data.length;j++) {
	entete = data[j];
	if (sep == "dotcoma") {	entete= replace(entete,";"," ");	entete = split(entete,"\ ");		} // if
	else if (sep == "tabs") {					entete = split(entete,"\t");		} // if
	else if (sep == "space") {					entete = split(entete,"\ ");		} // if
	object_list_B[0] = entete[1]+"_"+entete[0];
	vig_id = entete[entete.length - 1 ];
	// ------------- COPIE vignettes ---------------------
	if (process_opt == "text" && option == "new") {
		vigname  = object_list_B[0]+".jpg";
		read_file = path_work+"\\"+pid_file_short+"\\"+vigname;
		save_file_red = ecotaxa_folder + "\\"+ object_list_B[0];
		if (File.exists(read_file) && File.exists(save_file_red+".jpg") == false) {open(read_file);	saveAs("jpg",save_file_red);	close();	}// if
		else { nb_false++;	} // if 
	}// if
	// ------------ Variables dérivées -----------------------------
	if (entete.length > 15) {
		area = 		parseInt(entete[3]);	major = 	parseInt(entete[18]);	minor = 	parseInt(entete[19]);
		mean = 		parseInt(entete[4]);	min = 		parseInt(entete[7]);	max = 		parseInt(entete[8]);
		stddev = 	parseInt(entete[5]);	perim = 	parseInt(entete[13]);	area_exc = 	parseInt(entete[30]);
		feret = 	parseInt(entete[22]);	xm = 		parseInt(entete[11]);	ym = 		parseInt(entete[12]);
		x = 		parseInt(entete[9]);	y = 		parseInt(entete[10]);
	
		esd = 2*((area/PI)^0.5);				objet_derived_C[0] = esd;
		elongation = major/minor;				objet_derived_C[1] = elongation;
		range = max-min;					objet_derived_C[2] =	range;
		meanpos = (mean-max)/(mean-min);			objet_derived_C[3] = meanpos;
		centroids = ((xm-x)^2+(ym-y)^2)^0.5;			objet_derived_C[4] = centroids;
		cv = 	 100*(stddev/mean);				objet_derived_C[5] = cv;
		sr = 	 100*(stddev/(max-min));			objet_derived_C[6] = sr;
		perimareaexc = perim/(area_exc^0.5);			objet_derived_C[7] = perimareaexc;
		feretareaexc = feret/(area_exc^0.5);			objet_derived_C[8] = feretareaexc;
		perimferet = perim/feret;				objet_derived_C[9] = perimferet;
		perimmajor = perim/major;				objet_derived_C[10] = perimmajor;
		circexc = (4*PI*area_exc)/(perim^2);			objet_derived_C[11] = circexc;
		cdexc = (1/(area_exc^0.5)) * (((xm-x)^2+(ym-y)^2)^0.5);	objet_derived_C[12] = cdexc;
	} // if
		
	// -------- depth min et max ------------
	object_list_B[6] = entete[2];
	object_list_B[7] = entete[2];
	ligne = object_list_B[0]+".jpg"+"\t"+"0";
	for(i=0;i<object_list_B.length;i++) {		ligne = ligne +"\t"+object_list_B[i];			} // if	
	// ---------------- Lire entete data -----------------------------
	if (annotation_list_C[5] == "validated") {	long = entete.length - field_sup_nb;
		annotation_list_C[2] = vig_id; //entete[long];
	}// if
	if (status == true) annotation_list_C[5] == "predicted";
	for (k = 3; k<long ;k++) {			ligne = ligne +"\t"+toLowerCase(entete[k]);		} // for
	for(i=0;i<objet_derived_C.length;i++) {		ligne = ligne +"\t"+toLowerCase(objet_derived_C[i]);	} // if	
	for(i=0;i<annotation_list_C.length;i++) {	ligne = ligne +"\t"+toLowerCase(annotation_list_C[i]);	} // if
	for(i=0;i<process_list_B.length;i++) {		ligne = ligne +"\t"+toLowerCase(process_list_B[i]);	} // if
	for(i=0;i<acqu_list_B.length;i++) {		ligne = ligne +"\t"+toLowerCase(acqu_list_B[i]);	} // if
	for(i=0;i<sample_list_B.length;i++) {		ligne = ligne +"\t"+toLowerCase(sample_list_B[i]);	} // if
	data_ligne[m] = ligne;
	m++;
}//
// ------------------ Ecriture du fichier --------------------------- 
for (j = 0; j < m;j++) {	print(data_ligne[j]);	}// if

// ----------------- Enregistrement fichier TSV dans le même dossier ------------------------

selectWindow("Log");
save_tsv = "no";
aa = indexOf(pid_file,".");
ecotaxa_file = "ecotaxa_"+ substring(pid_file,0,aa-5)+"_"+annotation_list_C[5]+".tsv";
text =  "save=" + ecotaxa_folder + "\\" + ecotaxa_file;
if ((option == "maj" && File.exists(ecotaxa_folder + "\\" + ecotaxa_file)) || option == "new" ) {
	run("Text...",text);
	save_tsv = "yes";
}

// ----------------- Sauvegarde dans ECOTAXA -------------------------------------------------
text =  "save=" + projfolder+"\\ecotaxa\\" + ecotaxa_file;
if ((option == "maj" && File.exists(ecotaxa_root_folder + ecotaxa_file) )|| option == "new") {
	run("Text...",text);
	save_tsv = "yes";
}

run("Close");

// ----------------------- END ----------------------
ret = save_tsv;
return ret;
